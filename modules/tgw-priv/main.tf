
# Generated by Tofuinit
data "aws_availability_zones" "available" {
  state = "available"
}

data "aws_region" "current" {}

resource "aws_vpc" "main" {
  cidr_block           = var.vpc_cidr
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = var.vpc_name
  }
}

resource "aws_subnet" "private" {
  count                   = 3
  vpc_id                  = aws_vpc.main.id
  cidr_block              = cidrsubnet(var.vpc_cidr, 4, count.index + 1) // /24 subnets within /16 VPC
  map_public_ip_on_launch = false
  availability_zone       = element(data.aws_availability_zones.available.names, count.index)

  tags = {
    Name = "${var.vpc_name}-Private-${count.index + 1}"
  }
}


resource "aws_route_table" "private" {
  count = length(aws_subnet.private)

  vpc_id = aws_vpc.main.id

  tags = {
    Name = "${var.vpc_name}-Private-Route-Table"
  }
  depends_on = [
    aws_ec2_transit_gateway_vpc_attachment.vpcs_ops
  ]
}

resource "aws_route" "private_to_the_internet" {
  count                  = length(aws_subnet.private)
  route_table_id         = aws_route_table.private[count.index].id
  destination_cidr_block = "0.0.0.0/0"
  transit_gateway_id     = var.transit_gateway_id
  depends_on = [
    aws_ec2_transit_gateway_vpc_attachment.vpcs_ops
  ]
}

resource "aws_route" "ops" {
  count                  = length(aws_subnet.private)
  route_table_id         = aws_route_table.private[count.index].id
  destination_cidr_block = "10.0.0.0/8"
  transit_gateway_id     = var.transit_gateway_id
  depends_on = [
    aws_ec2_transit_gateway_vpc_attachment.vpcs_ops
  ]
}


resource "aws_route_table_association" "private" {
  count = length(aws_subnet.private)

  subnet_id      = aws_subnet.private[count.index].id
  route_table_id = aws_route_table.private[count.index].id
  depends_on = [
    aws_ec2_transit_gateway_vpc_attachment.vpcs_ops
  ]
}


# # Attach  TGW to ops vpscs
resource "aws_ec2_transit_gateway_vpc_attachment" "vpcs_ops" {
  subnet_ids         = [for s in aws_subnet.private : s.id]
  transit_gateway_id = var.transit_gateway_id
  vpc_id             = aws_vpc.main.id
  tags = {
    Name = "${var.vpc_name}-vpc"
  }
}


